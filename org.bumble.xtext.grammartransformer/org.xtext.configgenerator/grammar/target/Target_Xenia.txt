grammar com.foliage.xenia.Xenia with org.eclipse.xtext.common.Terminals

generate xenia "http://www.foliage.com/xenia/Xenia"

Model:
	headers+=Header
	entities+=Entity* 
	mapped_entities+=MappedEntity*;

SuperSite:
	SiteWithModal | Site
;

Header:
	'app' appName=ID 'has' 'pages' '['
		sites+=SuperSite (',' sites+=SuperSite)*
	']'
;

Entity:
	'with' 	':' tech=STRING |
	'xml' 	':' path=STRING |
	'mode' 	':' mode=Mode
;

MappedEntity:
	'info' ':' '[' infoProps+=InfoProperty (',' infoProps+=InfoProperty)* ']'	|
	'map'  ':' '[' linkedProps+=LinkedProperty (',' linkedProps+=LinkedProperty)* ']'
;

SiteWithModal:
	'@' name=ID 'with' 'modal' '(' sites+=SuperSite (',' sites+=SuperSite)* ')'
;

Site:
	'@' name=ID
;

enum Mode:
	DEV='development' | PROD='production'
;

InfoProperty:
	page=[Site] '->' entities+=InfoEntity (',' entities+=InfoEntity)*
;

LinkedProperty:
	name=[Site] '->' '('  page=RedirectPage ')'
;

InfoEntity:
	entries+=InfoEntry ':' infoValue=STRING
;

enum InfoEntry:
	MOD='mod' | FREQ='freq' | PRIO='prio'
;

RedirectPage:
	(site+=[Site]) (',' site+=[Site])* 
;